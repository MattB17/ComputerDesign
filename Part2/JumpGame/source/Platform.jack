// A representation of the platform on which the avatar lands

class Platform {
  // screen location of the platform's top-left corner.
  field int x_, y_;

  // represents the number of 16-bit segments wide the platform is.
  // that is, a value of 4 means the platform is 16 * 4 = 64 bits wide.
  field int width_blocks_;

  // the height of the platform.
  field int height_;

  // Constructs a new platform with its top left corner at (x, y) spanning
  // height rows and width_blocks blocks wide.
  constructor Platform new(int x, int y, int width_blocks, int height) {
    let x_ = x;
    let y_ = y;
    let width_blocks_ = width_blocks;
    let height_ = height;
    return this;
  }

  // Disposes of the platform.
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  // Returns the row corresponding to the top of the platform.
  method int getY() {
    return y_;
  }

  // Draws the platform on the screen.
  method void draw() {
    do drawInternal(-1);
    return;
  }

  // Erases the platform from the screen.
  method void erase() {
    do drawInternal(0);
    return;
  }

  // A helper method to draw the platform on the screen in the specified color:
  // -1 for black and 0 for white.
  method void drawInternal(int color) {
    var int row_count, block_count;
    var int curr_row, board_end;
    var int offset;

    let board_end = Constants.boardHeight() - 1;
    let curr_row = y_;
    let offset = Utils.getOffset(x_, curr_row);
    let row_count = 0;

    while (row_count < height_) {
      if ((~(curr_row < 0)) & (~(curr_row > board_end))) {
        let block_count = 0;
        while (block_count < width_blocks_) {
          do Memory.poke((offset + block_count), color);
          let block_count = block_count + 1;
        }
      }
      let offset = offset + Constants.blocksPerRow();
      let row_count = row_count + 1;
      let curr_row  = curr_row + 1;
    }

    return;
  }

  // Shifts the platform down num_rows rows.
  method void shiftDown(int num_rows) {
    let y_ = y_ + num_rows;
    return;
  }

  // Identifies whether the top of the platform is below the board. Used to
  // signal that the platform should be removed.
  method bool isBelowBoard() {
    return Utils.isBelowBoard(y_);
  }
}
