// A controller used to manage the Jumper throughout the game.

class JumperController {
  field Jumper jumper_; // the Jumper that is controlled by the user.

  // the shift size when the Jumper is stationary. If the Jumper is stationary
  // it is standing on a platform and so should shift down at the same speed
  // as the platform.
  field int stationary_shift_size_;

  // the shift size when the Jumper is in motion.
  field int moving_shift_size_;

  // identifies whether the jumper is currently in a jump.
  field bool in_jump_;

  // identifies the jumper's current horizontal direction.
  // a value of 0 indicates no horizontal movement, 1 for left, 2 for right.
  field int horizontal_direction_;

  // identifies if the jumper is moving upwards (at the start of a jump).
  // otherwise, the jumper is moving down, it is either falling at the end of
  // a jump or moving down while standing on a platform.
  field bool moving_up_;

  // used to track how high the jumper has jumped. This measures the upward
  // motion of the jumper (in units of 16-bit blocks) to signal when the jumper
  // will start to fall.
  field int jump_height_;

  constructor JumperController new(int stationary_shift_size) {
    let jumper_ = Jumper.new(
      400, 199, Constants.blockSize(), Constants.jumperBitMap());
    let stationary_shift_size_ = stationary_shift_size;
    let moving_shift_size_ = Constants.blockSize();
    let in_jump_ = false;
    let horizontal_direction_ = 0;
    let moving_up_ = false;
    let jump_height_ = 0;
    return this;
  }

  method void dispose() {
    do jumper_.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method bool isJumping() {
    return in_jump_;
  }

  method void drawJumper() {
    do jumper_.draw();
    return;
  }

  method void eraseJumper() {
    do jumper_.erase();
    return;
  }

  method void startJump() {
    let in_jump_ = true;
    let moving_up_ = true;
    let jump_height_ = 0;
    return;
  }

  method void setLeftMovement() {
    let horizontal_direction_ = 1;
    return;
  }

  method void setRightMovement() {
    let horizontal_direction_ = 2;
    return;
  }

  // returns a bool to signal whether the control loop was successful and the
  // jumper is still alive
  method bool runControlLoop() {
    var int gap_from_platform;
    do jumper_.erase();

    if (in_jump_) {
      if (moving_up_) {
        do jumper_.shiftUp(moving_shift_size_);
        // the jumper has moved up another 16-bit block.
        let jump_height_ = jump_height_ + 1;
      } else {
        let gap_from_platform = jumper_.gapFromPlatform();
        if (gap_from_platform < Constants.blockSize()) {
          do jumper_.shiftDown(gap_from_platform);
          let in_jump_ = false;
        } else {
          do jumper_.shiftDown(moving_shift_size_);
        }
      }
    } else {
      do jumper_.shiftDown(stationary_shift_size_);
    }

    if (horizontal_direction_ = 1) {
      if (in_jump_ | jumper_.onPlatformAfterLeftStep()) {
        do jumper_.shiftLeft(moving_shift_size_);
      } else {
        let horizontal_direction_ = 0;
      }
    }
    if (horizontal_direction_ = 2) {
      if (in_jump_ | jumper_.onPlatformAfterRightStep()) {
        do jumper_.shiftRight(moving_shift_size_);
      } else {
        let horizontal_direction_ = 0;
      }
    }

    // signals that the jumper has moved up 10 16-bit blocks and will start to
    // fall. 10 was chosen so that it is doable to land on the other platform.
    if (jump_height_ > 9) {
      let moving_up_ = false;
    }

    if (~(jumper_.isBelowBoard())) {
      do jumper_.draw();
      return true;
    }

    return false;
  }
}
