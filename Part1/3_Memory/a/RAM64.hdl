// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    // Implementation by Matt Buckley
    PARTS:
    // Idea: 3 most significant bits of address refer to which RAM8
    // unit and the 3 least significant bits refer to the particular
    // register within that RAM8

    // Use 8 way DMux to set the load bit to 0 for every
    // RAM8 other than the RAM8 referred to by the 3 most
    // significant bits of address
    DMux8Way(in=load, sel=address[3..5], a=load0, b=load1, c=load2, d=load3,
                                         e=load4, f=load5, g=load6, h=load7);

    // Then feed the input to the 8 RAM8 units with the
    // individual load bits and the first 3 bits of address
    RAM8(in=in, load=load0, address=address[0..2], out=outA);
    RAM8(in=in, load=load1, address=address[0..2], out=outB);
    RAM8(in=in, load=load2, address=address[0..2], out=outC);
    RAM8(in=in, load=load3, address=address[0..2], out=outD);
    RAM8(in=in, load=load4, address=address[0..2], out=outE);
    RAM8(in=in, load=load5, address=address[0..2], out=outF);
    RAM8(in=in, load=load6, address=address[0..2], out=outG);
    RAM8(in=in, load=load7, address=address[0..2], out=outH);

    // Lastly, use 16 bit 8 way Mux to select the output from the
    // appropriate RAM8 unit based on the 3 most significant bits
    // of address
    Mux8Way16(a=outA, b=outB, c=outC, d=outD, e=outE, f=outF,
              g=outG, h=outH, sel=address[3..5], out=out);
}
